include(CMakeParseArguments)
include(FetchContent)

FetchContent_Declare(
  super-build-depends
  GIT_REPOSITORY https://github.com/itsuhane/cmake-superbuild-depends.git
  GIT_TAG        master
)
FetchContent_GetProperties(super-build-depends)
if(NOT super-build-depends_POPULATED)
  message(STATUS "Fetching SuperBuildDepends scripts")
  FetchContent_Populate(super-build-depends)
  message(STATUS "Fetching SuperBuildDepends scripts - done")
endif()

list(APPEND CMAKE_MODULE_PATH "${super-build-depends_SOURCE_DIR}/cmake/Modules")

if(NOT TARGET options::modern-cpp)
  add_library(options::cpp17 INTERFACE IMPORTED GLOBAL)
  target_compile_features(options::cpp17 INTERFACE cxx_std_17)
  add_library(options::modern-cpp INTERFACE IMPORTED GLOBAL)
  target_link_libraries(options::modern-cpp INTERFACE options::cpp17)
endif()

if(NOT COMMAND superbuild_depend)
  function(superbuild_depend depend_name)
    include("${CMAKE_SOURCE_DIR}/cmake/external/${depend_name}.cmake")
  endfunction()
endif()

if(NOT COMMAND superbuild_extern)
  function(superbuild_extern extern_name)
    set(platform PLATFORM)
    cmake_parse_arguments(SUPERBUILD_EXTERN "" "${platform}" "" ${ARGN})
    if(NOT DEFINED SUPERBUILD_EXTERN_PLATFORM OR SUPERBUILD_EXTERN_PLATFORM STREQUAL AUTO)
      if(IOS)
        set(SUPERBUILD_EXTERN_PLATFORM IOS)
      elseif(ANDROID)
        set(SUPERBUILD_EXTERN_PLATFORM ANDROID)
      else()
        set(SUPERBUILD_EXTERN_PLATFORM SYSTEM)
      endif()
    endif()
    set(SUPERBUILD_EXTERN_SYSTEM_MODULE "${super-build-depends_SOURCE_DIR}/externs/${extern_name}.cmake")
    set(SUPERBUILD_EXTERN_IOS_MODULE "${super-build-depends_SOURCE_DIR}/externs/ios/${extern_name}.cmake")
    set(SUPERBUILD_EXTERN_ANDROID_MODULE "${super-build-depends_SOURCE_DIR}/externs/android/${extern_name}.cmake")
    if(EXISTS "${SUPERBUILD_EXTERN_${SUPERBUILD_EXTERN_PLATFORM}_MODULE}")
      include("${SUPERBUILD_EXTERN_${SUPERBUILD_EXTERN_PLATFORM}_MODULE}")
    else()
      include("${SUPERBUILD_EXTERN_SYSTEM_MODULE}")
    endif()
  endfunction()
endif()
